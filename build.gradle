buildscript {
    ext {
        generatedSourcesDir = "src/main/generated"

        // plugins
        projectName = 'Hiit'
        projectVersion = '1.0.1'
        springBootVersion = '2.7.5'
        dependencyManagementVersion = '1.0.15.RELEASE'
        spotlessVersion = '6.8.0'
        asciidoctorVersion = '3.3.2'
        epagesRestDocsApiSpecVersion = '0.16.0'
        sonarqubeVersion = '4.0.0.2929'

        // dependencies
        jsonwebtokenVersion = '0.11.5'
        flywayVersion = '9.16.0'
        jsr305Version = '3.0.1'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${dependencyManagementVersion}"

    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'

    id "com.diffplug.spotless" version "${spotlessVersion}"

    // code-quality plugins
    id 'org.asciidoctor.jvm.convert' version "${asciidoctorVersion}"
    id 'com.epages.restdocs-api-spec' version "${epagesRestDocsApiSpecVersion}"
    id "org.sonarqube" version "${sonarqubeVersion}"
    id 'jacoco'
}


group = 'com.hiit'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}
/** apply dependencies */
apply from: './integration-test.gradle'

/** apply tasks */
apply from: './tasks/build-task.gradle'
apply from: './tasks/formatting-task.gradle'
apply from: './tasks/code-quality-task.gradle'
apply from: './tasks/docs-task.gradle'
apply from: './tasks/install-git-hooks.gradle'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java'

    implementation 'org.hibernate:hibernate-envers'

    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'javax.annotation:javax.annotation-api'

    annotationProcessor("javax.persistence:javax.persistence-api")

    // querydsl
    implementation 'com.querydsl:querydsl-jpa'
    implementation 'com.querydsl:querydsl-core'
    implementation 'com.querydsl:querydsl-apt'

    // jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: "${jsr305Version}"

    // apache commons
    implementation 'org.apache.commons:commons-lang3:3.0'

    // guava
    implementation 'com.google.guava:guava:32.1.2-jre'

    //jwt
    implementation "io.jsonwebtoken:jjwt-api:${jsonwebtokenVersion}"
    implementation "io.jsonwebtoken:jjwt-impl:${jsonwebtokenVersion}"
    implementation "io.jsonwebtoken:jjwt-jackson:${jsonwebtokenVersion}"

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // infra
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // flyway
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation 'org.flywaydb:flyway-mysql'
}

test {
    useJUnitPlatform()

    testLogging {
        events "failed"
        exceptionFormat "full"
    }

    finalizedBy jacocoTestReport
}

// querydsl config
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {
    main.java.srcDir querydslDir
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
